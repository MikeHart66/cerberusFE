Strict
#GLFW_WINDOW_TITLE="2D in 3D Example"
#GLFW_WINDOW_WIDTH=1920			
#GLFW_WINDOW_HEIGHT=1080	
'#GLFW_WINDOW_RESIZABLE=False
'#GLFW_WINDOW_FULLSCREEN=True

#OPENGL_DEPTH_BUFFER_ENABLED=True
#OPENGL_GLES20_ENABLED=True

Import mojo2
Import graphics3d

#If TARGET="glfw"
Import brl.process           ' For ExitApp
#End


'*******************************************************************    
' Define the myClass that extends the App Class.    
Class myClass Extends App
	Field _cW:Int
	Field _cH:Int
	Field canv3D:Canvas3D
	Field canvUI:Canvas
	Field drwLstUI:DrawList
	Field drwLst3D:DrawList

	Field imgLogo:Image
	Field imgTiles:Image[]
	Field imgPlant:Image

	Field camx:Float = 0
	Field camy:Float = -80
	Field camz:Float = -140
	
	Field stime:Int = Millisecs()
	Field ltime:Int
	Field delta:Float
	Field etime:Int
	Field dtime:Int
	Field iFPS:Int
	Field FPS:Int = 60
	
	'---------------------------------------------------------------
    Method OnCreate:Int()
        SetUpdateRate(0)
        SetSwapInterval(0)
        
        _cW = DeviceWidth()
        _cH = DeviceHeight()
        imgLogo = Image.Load("logoCX.png",0.5,0.5)
        imgPlant = Image.Load("plant.png",0.5,1.0,0)
        imgTiles = Image.LoadFrames("tiles.png",4, False,0.5,0.5,0)

		canvUI = New Canvas
		drwLstUI = New DrawList
		
        canv3D = New Canvas3D
		drwLst3D = New DrawList
        
        canv3D.SetProjection3D(45 , DeviceWidth()/Float(DeviceHeight()), 0.1, 30000)
		canv3D.SetCamLookAt(camx, camy, camz,  0, 0, 0,  [0.0, 1.0, 0.0] )

        Return 0
    End
    
	'---------------------------------------------------------------
    Method OnUpdate:Int()
 		iFPS += 1
		etime = Millisecs()
		dtime = etime - stime
		If dtime >= 1000
			FPS = iFPS
			iFPS = 0
			stime = etime
		Endif
		delta = etime-ltime
		ltime = etime
		
		Local deltamod:Float = 4.0 * delta/60.0
		
    	If (KeyDown(KEY_LEFT) Or (TouchDown(0)=1 And TouchX(0) < _cW/2)) Then camx-= deltamod
    	If (KeyDown(KEY_RIGHT) Or (TouchDown(0)=1 And TouchX(0) >= _cW/2)) Then camx+= deltamod
    	If (KeyDown(KEY_UP) Or (TouchDown(0)=1 And TouchY(0) < _cH/2)) Then camz+= deltamod
    	If (KeyDown(KEY_DOWN) Or (TouchDown(0)=1 And TouchY(0) >= _cH/2)) Then camz-= deltamod
    	
    	If KeyDown(KEY_PAGEUP) Then camy+= deltamod
    	If KeyDown(KEY_PAGEDOWN) Then camy-= deltamod
    	If KeyDown(KEY_SPACE)
    		camx = 0.0
    		camy = -80.0
			camz = -140.0
		Endif
		canv3D.SetCamLookAt(camx, camy, camz,     camx+30, 0, camz+140.0, [0.0, 1.0, 0.0])
		
#If TARGET="glfw"
    	If KeyDown(KEY_ESCAPE) Then ExitApp(0)
#End   	
		
        Return 0
    End
    
	'---------------------------------------------------------------
    Method OnRender:Int()

		canvUI.Clear()
		drwLstUI.Reset()
		drwLst3D.Reset()
		
        canv3D.Init3D()
        canv3D.Clear(0 ,0,.5)	
		canv3D.PushMatrix()
		canv3D.SetColor(1,1,1)  
		      
        Local items:Int
        Local items2:Int
        Local size:Int=1024
        'Ground
        Seed=0
		For Local z:Int = size To -size Step -16
			For Local x:Int = -size To size Step 16
	        	drwLst3D.DrawImage(imgTiles[Int(Rnd(0,4))],x,z)
	        	items+=1
	        Next
        Next
        canv3D.SetModelMatrix3D(0,0,0, 90.0,0.0,0.0, 1.0,1.0,1.0)
        canv3D.RenderDrawList(drwLst3D)

        'Plants
        Seed=1563
		For Local z:Int = size To -size Step -16
			'canv3D.SetModelMatrix3D(0,0,z+8, 0.0,0.0,0.0, 1.0,1.0,1.0)
			For Local x:Int = -size To size Step 16
	        	If Int(Rnd(1,100)) >= 90
		        	'canv3D.DrawImage(imgPlant,x,0.0) 
		        	canv3D.DrawImage3D(imgPlant,x,0.0, z+8,-25,0,0) 
		        	items2+=1
				Endif
	        Next
        Next

        canv3D.PopMatrix()

		' Draw  UI
        drwLstUI.SetColor(1,1,1)
        drwLstUI.DrawText( "Camera position = "+Int(camx)+":"+Int(camy)+":"+Int(camz), 10,10, 0.0,0.5)
        drwLstUI.DrawText( "FPS = "+FPS+"   ( tiles="+items+" : items="+items2+" )", 10,25, 0.0,0.5)

        drwLstUI.DrawText( "Press Cursor-Keys and PageUp/-Down to move the camera. Press SPACE to reset the view.", 10,_cH-10 , 0.0,0.5)
        drwLstUI.SetAlpha(0.5)
        drwLstUI.DrawImage(imgLogo,_cW-60,_cH-60,0,0.4,0.4)
        drwLstUI.SetAlpha(1.0)

        canvUI.RenderDrawList(drwLstUI)
        
        canv3D.Flush()
		canvUI.Flush()
		
        Return 0
    End
End

'---------------------------------------------------------------
Function Main:Int()
    New myClass		
    Return 0
End
